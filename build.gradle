plugins {
    id 'maven-publish'
    id 'signing'
    id 'java-library'
    id "com.diffplug.spotless" version "5.1.1"
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
    id 'io.codearte.nexus-staging' version '0.21.2'
    id 'com.palantir.git-version' version '0.12.3'
}

group 'io.leonard'
version gitVersion()

sourceCompatibility = 11

repositories {
    jcenter()
    mavenCentral()
}

compileJava {
    options.compilerArgs << '-parameters'
}

ext {
    junitVersion = '5.6.2'
    parserVersion = '0.22.0'
}

dependencies {
    implementation group: 'ch.poole', name: 'OpeningHoursParser', version: '0.23.2'

    testImplementation  group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    // because we bundle everything up as a fat jar, this needs to be here a second time
    testImplementation group: 'ch.poole', name: 'OpeningHoursParser', version: parserVersion
}

spotless {
    java {
        importOrder()
        removeUnusedImports()

        googleJavaFormat()
    }
}

jar {
    from {
        // by using the compile time classpath we add the parser classes to the jar but the dependency doesn't end up
        // in the POM's <dependency> block
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

//-- begin maven-publish.publish  spell --
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'OpenStreetMap Opening Hours evaluator'
                description = 'Evaluator for OpenStreetMap-formatted opening hours'
                url = 'https://github.com/leonardehrenfried/opening-hours-evaluator'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'leonardehrenfried'
                        name = 'Leonard Ehrenfried'
                        email = 'mail@leonard.io'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/leonardehrenfried/opening-hours-evaluator.git'
                    developerConnection = 'scm:git:https://github.com/leonardehrenfried/opening-hours-evaluator.git'
                    url = 'https://github.com/leonardehrenfried/opening-hours-parser'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('nexusUsername') ?: 'secured'
                password project.findProperty('nexusPassword') ?: 'secured'
            }
        }
    }
}

nexusStaging {
    delayBetweenRetriesInMillis = 15 * 1000
}

signing {
    sign publishing.publications
}